PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX ipdc-lpdc: <https://productencatalogus.data.vlaanderen.be/ns/ipdc-lpdc#>
PREFIX nuts:  <http://data.europa.eu/nuts/code/>

DELETE {
  GRAPH ?g {
    ?productInstance dcterms:spatial ?oldNutsCode.
  }
} INSERT {
  GRAPH ?g {
    ?productInstance dcterms:spatial ?newNutsCode.
  }
} WHERE {
  GRAPH ?g {
    ?productInstance a ipdc-lpdc:InstancePublicService;
                     dcterms:spatial ?oldNutsCode;
                     ipdc-lpdc:forMunicipalityMerger """true"""^^<http://www.w3.org/2001/XMLSchema#boolean>.
  }

  VALUES ?g {
    <http://mu.semte.ch/graphs/organizations/19483103-318e-435a-aa37-45e485406ee9/LoketLB-LPDCGebruiker> # Beveren-Kruibeke-Zwijndrecht (Municipality)
    <http://mu.semte.ch/graphs/organizations/99da98a7a0087d3429b084ebfc4eb5d488c593790d4d5af7253982a2e21a6a5f/LoketLB-LPDCGebruiker> # Bilzen (Municipality)
    <http://mu.semte.ch/graphs/organizations/026509cf3b4eeb7ad88fe57a270060574f60abd1c3524837d36700e40809d210/LoketLB-LPDCGebruiker> # Hasselt (OCMW)
    <http://mu.semte.ch/graphs/organizations/9db1b46874a57fe63c08fb5f16b117e6f61fdd98e7f64f745d0fceb9d3731169/LoketLB-LPDCGebruiker> # Hasselt (Municipality)
    <http://mu.semte.ch/graphs/organizations/2ac1bb2a7d7bbd98e2e7a24be2c67e42171788a71c2436a33060626593bb2f78/LoketLB-LPDCGebruiker> # Lochristi (Municipality)
    <http://mu.semte.ch/graphs/organizations/cb2a6e0a490ee881ddd0d9ded7f2b3d1dc2df7e57a19d014caac054bfa355f5a/LoketLB-LPDCGebruiker> # Lokeren (Municipality)
    <http://mu.semte.ch/graphs/organizations/323a24f2fe81ee0b6586ec78be36760e478092e07386b2785992ea8b61941833/LoketLB-LPDCGebruiker> # Lokeren (OCMW)
    <http://mu.semte.ch/graphs/organizations/b8bb293d-aa22-4b43-bda4-0b6af76e9493/LoketLB-LPDCGebruiker> # Merelbeke-Melle (Municipality)
    <http://mu.semte.ch/graphs/organizations/1ca65d65-54ff-4b44-b750-bd70c91191af/LoketLB-LPDCGebruiker> # Nazareth-De Pinte (Municipality)
    <http://mu.semte.ch/graphs/organizations/b36da606fba6dd4dc99ae1ef5f4a52bba3268d33f4bc2cd1e65b87f01f35101a/LoketLB-LPDCGebruiker> # Tielt (Municipality)
    <http://mu.semte.ch/graphs/organizations/104f32d7fb8d4b8b61b71717301656f136fe046eabaf126fb3325896b5c2d625/LoketLB-LPDCGebruiker> # Tongeren (Municipality)
    <http://mu.semte.ch/graphs/organizations/99ed6eee81a7aca47517cbffb46eaba38f3987eeb4ad32c020898644769eb615/LoketLB-LPDCGebruiker> # Wingene (Municipality)

  }

  VALUES (?oldNutsCode ?newNutsCode) {
    (nuts:BE21111056 nuts:BE23646030) # Zwijndrecht -> Beveren-Kruibeke-Zwijndrecht
    (nuts:BE22373006 nuts:BE22373110) # Bilzen -> Bilzen-Hoeselt
    (nuts:BE22373009 nuts:BE22373111) # Borgloon -> Tongeren-Borgloon
    (nuts:BE22373083 nuts:BE22373111) # Tongeren -> Tongeren-Borgloon
    (nuts:BE23444048 nuts:BE23444086) # Nazareth -> Nazareth-De Pinte
    (nuts:BE22171022 nuts:BE22171072) # Hasselt (voor fusie 2025) -> Hasselt
    (nuts:BE23444073 nuts:BE23444087) # Wachtebeke -> Lochristi
    (nuts:BE23444034 nuts:BE23444087) # Lochristi (voor fusie 2025) -> Lochristi
    (nuts:BE23646014 nuts:BE23646029) # Lokeren (voor fusie 2025) -> Lokeren
    (nuts:BE23444043 nuts:BE23444088) # Merelbeke -> Merelbeke-Melle
    (nuts:BE25737007 nuts:BE25737022) # Meulebeke -> Tielt
    (nuts:BE25737015 nuts:BE25737022) # Tielt (voor fusie 2025) -> Tielt
    (nuts:BE25737012 nuts:BE25737021) # Ruiselede -> Wingene
    (nuts:BE25737018 nuts:BE25737021) # Wingene (voor fusie 2025) -> Wingene
  }
}
