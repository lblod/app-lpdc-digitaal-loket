PREFIX mu:      <http://mu.semte.ch/vocabularies/core/>
PREFIX ext:     <http://mu.semte.ch/vocabularies/ext/>
PREFIX skos:    <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX foaf:    <http://xmlns.com/foaf/0.1/>

INSERT {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?persoon a foaf:Person ;
      mu:uuid ?uuidPersoon ;
      foaf:firstName ?classificatie ;
      foaf:familyName ?naam ;
      foaf:member ?bestuurseenheid ;
      foaf:account ?account .

    ?account a foaf:OnlineAccount ;
      mu:uuid ?uuidAccount ;
      foaf:accountServiceHomepage <https://github.com/lblod/mock-login-service> ;
      ext:sessionRole "LoketLB-LPDCGebruiker" .
  }

  GRAPH ?g {
    ?persoon a foaf:Person ;
      mu:uuid ?uuidPersoon ;
      foaf:firstName ?classificatie ;
      foaf:familyName ?naam ;
      foaf:member ?bestuurseenheid ;
      foaf:account ?account .

    ?account a foaf:OnlineAccount ;
      mu:uuid ?uuidAccount ;
      foaf:accountServiceHomepage <https://github.com/lblod/mock-login-service> ;
      ext:sessionRole "LoketLB-LPDCGebruiker" .
  }
}
WHERE {
  ?bestuurseenheid a besluit:Bestuurseenheid ;
    mu:uuid ?uuidOrganization ;
    skos:prefLabel ?naam ;
    besluit:classificatie/skos:prefLabel ?classificatie .

  # Filter for District Borsbeek
  FILTER (?uuidOrganization = "4b44e6f1-113b-4692-b149-44a889b215f2")

  FILTER NOT EXISTS {
    ?person foaf:member ?bestuurseenheid .
  }

  BIND(CONCAT(?classificatie, " ", ?naam) AS ?volledigeNaam)
  BIND(MD5(?volledigeNaam) AS ?uuidPersoon)
  BIND(MD5(CONCAT(?volledigeNaam,"ACCOUNT")) AS ?uuidAccount)
  BIND(IRI(CONCAT("http://data.lblod.info/id/persoon/", ?uuidPersoon)) AS ?persoon)
  BIND(IRI(CONCAT("http://data.lblod.info/id/account/", ?uuidAccount)) AS ?account)
  BIND(IRI(CONCAT("http://mu.semte.ch/graphs/organizations/", ?uuidOrganization)) AS ?g)
}
