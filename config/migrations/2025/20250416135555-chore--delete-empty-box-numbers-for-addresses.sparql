PREFIX schema: <http://schema.org/>
PREFIX ipdc-lpdc: <https://productencatalogus.data.vlaanderen.be/ns/ipdc-lpdc#>
PREFIX euro: <http://data.europa.eu/m8g/>
PREFIX adres: <https://data.vlaanderen.be/ns/adres#>

DELETE {
  GRAPH ?g {
    ?address adres:Adresvoorstelling.busnummer ?boxNumber .
  }
} WHERE {
  GRAPH ?g {
    ?s a ipdc-lpdc:InstancePublicService ;
       euro:hasContactPoint ?contactPoint .
    ?contactPoint a schema:ContactPoint ;
                  ipdc-lpdc:address ?address .
    ?address adres:Adresvoorstelling.busnummer ?boxNumber .
    FILTER REGEX(STR(?boxNumber), "^\\s*$")
  }
  FILTER (STRSTARTS(STR(?g), "http://mu.semte.ch/graphs/organizations/"))
}
